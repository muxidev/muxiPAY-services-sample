apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from: rootProject.file("dependencies.gradle")

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        return ""
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "muxi.kotlinsample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        def TYPE_STRING = "String"
        def API_KEY = "API_KEY"
        it.buildConfigField TYPE_STRING, API_KEY, "\""+getProps(API_KEY)+"\""
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation(
            supportLibraries.values(),
            remoter.annotations,
            lottie.values(),
            anko.values(),
            kotlin.values(),
            materialDesign.values()
    )

    implementation project(':mpservices-sdk')

}
